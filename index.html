<!--
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <https://www.gnu.org/licenses/>.
-->
<!DOCTYPE html>
<html>
  <head>
    <title>My shaders Proyect by SirArthurDayne</title>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8">
    <script src="webgl-driver.js" type="text/javascript"></script>
    <link href='https://fonts.googleapis.com/css?family=Josefin Slab' rel='stylesheet'>
    <style>
        body {
            background-color: black;
            color: white;
            font-family: 'Josefin Slab';font-size: 32px;
        }
        canvas.gl {
          position:fixed;
          z-index:-1;
          left:0;
          top:0;
          width:100%;
          height:100%;
        }
    </style>
    <script id="shader-fs" type="x-shader/x-fragment">
      // -----------------------------------------------------------------------
      // BEGIN - Common prelude
      // -----------------------------------------------------------------------
      precision mediump float;

      uniform vec2 iResolution;
      uniform float iTime;
      uniform sampler2D iChannel0;

      varying highp vec2 vTextureCoord;

      void mainImage(out vec4 fragColor, in vec2 fragCoord);

      void main(void) {
        mainImage(gl_FragColor, vTextureCoord*iResolution);
      }
      // -----------------------------------------------------------------------
      // END - Common prelude
      // -----------------------------------------------------------------------

      // Created by sirArthurDayne
      // License Creative Commons Attribution-NonCommercial-ShareAlike 3.0 Unported License.

      // Based upon: https://www.shadertoy.com/view/3sKGz1
      
#define PI 3.141592
#define TAU (2.0*PI)


vec2 ConvertToPolar(vec2 rectCoord)
{
    //vectors becomes: magnitude, angle
    return vec2(length(rectCoord), atan(rectCoord.y, rectCoord.x));
}

vec2 ConvertToRect(vec2 polarCoord)
{
    return vec2(polarCoord.x * cos(polarCoord.y), polarCoord.x * sin(polarCoord.y));
}

vec3 ColorTransition(vec3 colorA, vec3 colorB)
{
    float pct = abs(sin(iTime))/2.0;

    return vec3(mix (colorA, colorB, pct));
}


vec2 pMod2(inout vec2 p, vec2 size) 
{
    vec2 c = floor((p + size * 0.5) / size);
    p = mod(p + size * 0.5, size) - size * 0.5;
    return c;
}


float DistanceCircle(float radius, vec2 origin)
{
    return length(origin) - radius;
}

float DistanceRoundedBox(float radius, vec2 origin)
{
    /*the more pow increment the less rounded the box is*/
    origin = origin * origin;
    origin = origin * origin;
    float d8 = dot(origin, origin);
    return pow(d8, 1.0 / 8.0) - radius;
}

float DistanceBox(vec2 size, vec2 origin)
{
    origin = abs(origin);
    float dx = 2.0*origin.x - size.x;
    float dy = 2.0*origin.y - size.y;
    return max(dx, dy);
}

float DrawMotionOne(vec2 position)
{
    position = abs(position);
    vec2 p_position = ConvertToPolar(position);
    p_position.y += position.x * sin(iTime);//rotation
    p_position.x *= 1.0 + position.y;//mod radius
    position = ConvertToRect(p_position);
    float distanceThree = DistanceBox(vec2(0.5, 0.5), position + vec2(0.0));
    return distanceThree;
}

float DrawCaleidoscopeSimple(vec2 position, float times)
{
    vec2 p_pos = ConvertToPolar(position);
    p_pos.y = mod(p_pos.y, TAU / times);
    p_pos.y += iTime;
    position = ConvertToRect(p_pos);
    pMod2(position, vec2(0.5));
    float d1 = DistanceCircle(0.2, position);
    float d2 = DistanceBox(vec2(0.2), position - vec2(0.1));
    return min(d1, d2);
}

float DrawCaleidoscopeEffect(vec2 position, float times, vec2 size)
{
    vec2 p_pos = ConvertToPolar(position);
    float beta = TAU / times;
    float np = p_pos.y / beta;
    p_pos.y = mod(p_pos.y, beta);
    float m2 = mod(np, 2.0);
    if (m2 > 1.0)
    {
        p_pos.y = beta - p_pos.y;
    }
    p_pos.y += iTime;
    position = ConvertToRect(p_pos);

    //make repeating patron
    pMod2(position, size);

    float d1 = DistanceCircle(0.1, position);
    float d2 = DistanceBox(vec2(0.1), position - vec2(0.1));
    return min(d1, d2);
}
//post processing methods
void Rotation(inout vec2 position, float angle)
{
    position = vec2(position.x * cos(angle) + position.y * sin(angle), 
        -position.x * sin(angle) + position.y * cos(angle));
}

vec3 ChangeSaturation(vec3 color, vec2 position)
{
    Rotation(position, iTime);
    color = clamp(color, 0.0, 1.0);
    return pow(color, vec3(length(position)));//more saturate at borders
    
    //return pow(color, vec3(1.0 /length(position)));//more saturate at center
    //saturate roullete
    //return pow(color, vec3(abs(position.x)/ length(position), abs(position.y)/ length(position),length(position)));
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{   
    vec3 crimson =  vec3(0.7, 0.02, 0.23);
    vec3 elecgreen = vec3(0.04, 1.0, 0.16);
    vec3 outputColor = vec3(0.0);

    //setup scaling and origin pos
    vec2 uv = fragCoord/iResolution.xy - vec2(0.5);
    uv.x *= iResolution.x / iResolution.y;
    //uv.y *= iResolution.x / iResolution.y;
    

    
    vec2 offset = uv;
    Rotation(offset, iTime);

    float distance = DrawCaleidoscopeEffect(uv-offset, 25.0, vec2(0.5));
    float md = mod(distance, 0.1);
    float nd = abs(distance / 0.1) ;

    if (abs(distance) < 0.1)
    {
        outputColor = ColorTransition(crimson, elecgreen);
    }
    
    if (abs(md) < 0.01)
    {
        outputColor = (distance < 0.0) ? crimson / nd : elecgreen / nd;
    }
    
    //apply postProccessing before outputing 
    
    fragColor = vec4(ChangeSaturation(outputColor, uv), 1.0);
}


     

     
    </script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute highp vec3 aVertexPosition;
      attribute highp vec3 aVertexNormal;
      attribute highp vec2 aTextureCoord;

      varying highp vec2 vTextureCoord;
      varying highp vec3 vNormal;

      void main(void) {
        gl_Position   = vec4(aVertexPosition, 1.0);
        vNormal = aVertexNormal;
        vTextureCoord = aTextureCoord;
      }
    </script>
  </head>

  <body onload="start()">
    <!--
      dont need this, support for texture images 
      <img id="image0" src="essenbee.png" style="visibility: collapse"/>
    -->
    <canvas id="glcanvas" class="gl">
      Your browser doesn't appear to support the HTML5 <code>&lt;canvas&gt;</code> element.
    </canvas>
  </body>
</html>